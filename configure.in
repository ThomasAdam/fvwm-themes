dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.12)
AC_REVISION(0.2.3)
AC_INIT(themes/default/README)
AM_INIT_AUTOMAKE(fvwm-themes, 0.4.2)

REQUIRED_FVWM_VERSION=2.3.22
AC_SUBST(REQUIRED_FVWM_VERSION)dnl

mg_ARG_WITH(fvwm-bindir, [  --with-fvwm-bindir DIR  specify fvwm bin directory, if not in PATH], FVWM_BINDIR, "")
mg_ARG_WITH(ft-datadir,  [  --with-ft-datadir DIR   specify data dir, if different from fvwm], FT_DATADIR, "")

AC_ARG_ENABLE(gnome-icons, [  --enable-gnome-icons    convert gnome icons to xpm icons ],GNOME_ICONS="yes",GNOME_ICONS="no")
AC_SUBST(GNOME_ICONS)dnl

AC_ARG_ENABLE(kde2-icons, [  --enable-kde2-icons     convert kde2 icons to xpm icons ],KDE2_ICONS="yes",KDE2_ICONS="no")
AC_SUBST(KDE2_ICONS)dnl

AC_ARG_ENABLE(build-menus, 
  [  --disable-build-menus   disable the rebuilding of the user menus ],
  [ if test "$enableval" = yes; then
      BUILD_MENUS="yes"
    else
      BUILD_MENUS="no"
    fi ],
  [ BUILD_MENUS="yes" ])
AC_SUBST(BUILD_MENUS)dnl

AC_PATH_PROG(PERL, perl)
if test -z "$PERL"; then
	AC_MSG_WARN(Perl is not found; you will have fatal problems using this package!)
	PERL=/usr/bin/perl
fi
AC_SUBST(PERL)dnl

AC_PATH_PROG(MKFIFO, mkfifo)
if test -z "$MKFIFO"; then
	AC_MSG_WARN(mkfifo is not found; you will have some problems with GUI!)
fi

# we check for FvwmM4 below
AC_PATH_PROG(M4, m4)
if test -z "$M4"; then
	AC_MSG_WARN(m4 is not found; you will have a few problems using this package!)
fi

# fvwm-themes-images needs to know where the rgb.txt file is
AC_PATH_XTRA
if test -z "$X_LIBS"; then
	AC_MSG_WARN(Xlib is not found; fvwm-themes-images will not fully work!)
fi

# FIXME: a better way to find GNOME_IMAGES_DIR?
# I've no gnome-config on my machine.
AC_PATH_PROG(GNOME_SESSION, gnome-session)
if test -z "$GNOME_SESSION"; then
	GNOME_SESSION=/usr/bin/gnome-session
fi
GNOME_IMAGES_DIR=`echo $GNOME_SESSION | sed -e "s,/[[^/]]*/gnome-session$,,"`/share/pixmaps
AC_SUBST(GNOME_IMAGES_DIR)dnl

AC_PATH_PROG(FVWM_CONFIG, fvwm-config --version, , "${PATH}:$FVWM_BINDIR")
if test -z "$FVWM_CONFIG"; then
	AC_MSG_ERROR([
	fvwm-config is not found; try --with-fvwm-bindir
	You need at least fvwm-$REQUIRED_FVWM_VERSION])
fi
AC_SUBST(FVWM_CONFIG)dnl

FVWM_BINDIR=`$FVWM_CONFIG --bindir`
AC_SUBST(FVWM_BINDIR)dnl
FVWM_DEFAULT_USERDIR=`$FVWM_CONFIG --default-userdir`
AC_SUBST(FVWM_DEFAULT_USERDIR)dnl
FVWM_DEFAULT_IMAGEPATH=`$FVWM_CONFIG --default-imagepath`
AC_SUBST(FVWM_DEFAULT_IMAGEPATH)dnl
FVWM_MODULEDIR=`$FVWM_CONFIG --fvwm-moduledir`

AC_PATH_PROG(FVWM_M4, FvwmM4, ,"$FVWM_MODULEDIR")
if test -z "$FVWM_M4"; then
	AC_MSG_WARN(FvwmM4 is not found; you will have a few problems using this package!)
fi

FVWM_VERSION=`$FVWM_CONFIG --version`
if test "x$FVWM_VERSION" = "x"; then FVWM_VERSION=unknown; fi
AC_SUBST(FVWM_VERSION)dnl

if awk "BEGIN { exit($FVWM_VERSION >= $REQUIRED_FVWM_VERSION) }"; then
	AC_MSG_ERROR([
	You have fvwm-$FVWM_VERSION, which is not up to date
	You need at least fvwm-$REQUIRED_FVWM_VERSION])
fi

ROOT_PREFIX=""
AC_SUBST(ROOT_PREFIX)dnl

if echo "${prefix}NONE" | awk '/^NONE/ { exit 1 }'; then
	mg_DEFAULT_DIR_ARG(bindir,     '${prefix}/bin')
	mg_DEFAULT_DIR_ARG(mandir,     '${prefix}/man')
	mg_DEFAULT_DIR_ARG(FT_DATADIR, '${datadir}/fvwm')
else
	mg_DEFAULT_DIR_ARG(bindir,     '${ROOT_PREFIX}'"`$FVWM_CONFIG --bindir`")
	mg_DEFAULT_DIR_ARG(mandir,     '${ROOT_PREFIX}'"`$FVWM_CONFIG --mandir`")
	mg_DEFAULT_DIR_ARG(FT_DATADIR, '${ROOT_PREFIX}'"`$FVWM_CONFIG --fvwm-datadir`")
fi

#FT_FVWMSCRIPT_DIR='${FT_DATADIR}/scripts'
#FT_FVWMFORM_DIR='${FT_DATADIR}/forms'
FT_FVWMSCRIPT_DIR='${FT_DATADIR}'
FT_FVWMFORM_DIR='${FT_DATADIR}'
FT_LOCALE_DIR='${FT_DATADIR}/locale'

AC_SUBST(FT_DATADIR)dnl
AC_SUBST(FT_FVWMSCRIPT_DIR)dnl
AC_SUBST(FT_FVWMFORM_DIR)dnl
AC_SUBST(FT_LOCALE_DIR)dnl

#FT_VERSION_INFO="FVWM Themes version $VERSION from `date +'%d-%b-%Y %T'`"
# migo: to match the one in FVWM...
FT_VERSION_INFO="FVWM Themes version $VERSION from `date +'%b %d %Y at %T'`"
AC_SUBST(FT_VERSION_INFO)dnl

SETTINGS_FILE='${FT_DATADIR}/themes/default/settings.cfg'
AC_SUBST(SETTINGS_FILE)dnl

AC_OUTPUT(
	Makefile
	bin/Makefile
	doc/Makefile
	forms/Makefile
	scripts/Makefile
	locale/Makefile
	locale/en/Makefile
	locale/fr/Makefile
	locale/ru/Makefile
	bin/fvwm-themes-config
	bin/fvwm-themes-start
	bin/fvwm-themes-xrdb
	bin/fvwm-themes-menuapp
	bin/fvwm-themes-com
	bin/fvwm-themes-script
	bin/fvwm-themes-images
	doc/fvwm-themes.lsm
	rpm/fvwm-themes.spec
	rpm/Makefile
)

if test -n "$DEBUG"; then
	echo '------------ ( Debug ) ------------'
	echo "[prefix:            ${prefix}]"
	echo "[bindir:            ${bindir}]"
	echo "[mandir:            ${mandir}]"
	echo "[ft-datadir:        ${FT_DATADIR}]"
	echo "[ft-fvwmscript-dir: ${FT_FVWMSCRIPT_DIR}]"
	echo "[ft-fvwmform-dir:   ${FT_FVWMFORM_DIR}]"
	echo "[gnome-icons:       ${GNOME_ICONS}]"
	echo "[gnome-session:     ${GNOME_SESSION}]"
	echo "[gnome-images-dir:  ${GNOME_IMAGES_DIR}]"
	echo "[kde2-icons:        ${KDE2_ICONS}]"
	echo "[settings-file:     ${SETTINGS_FILE}]"
	echo "[x-libs:            ${X_LIBS}]"
	echo "[x-extra-libs:      ${X_EXTRA_LIBS}]"
	echo '-----------------------------------'
fi

echo "
=============================================================================
FVWM Themes Version:	$VERSION
FVWM Version:		$FVWM_VERSION
=============================================================================

Run 'make; su; make install' to install $PACKAGE.
"
